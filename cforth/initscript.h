": DECIMAL 10 BASE ! ;\r"
        ": HEX 16 BASE ! ;\r"
        ": OCTAL 8 BASE ! ;\r"
        ": 2DUP OVER OVER ;\r"
        ": 2DROP DROP DROP ;\r"
        ": NIP SWAP DROP ;\r"
        ": 2NIP 2SWAP 2DROP ;\r"
        ": TUCK SWAP OVER ;\r"
        ": / /MOD NIP ;\r"
        ": U/ U/MOD NIP ;\r"
        ": MOD /MOD DROP ;\r"
        ": UMOD U/MOD DROP ;\r"
        ": BL 32 ;\r"
        ": CR 10 EMIT ;\r"
        ": SPACE BL EMIT ;\r"
        ": INVERT NOT ;\r"
        ": NEGATE 0 SWAP - ;\r"
        ": DNEGATE 0. 2SWAP D- ;\r"
        ": CELLS CELL * ;\r"
        ": ALLOT HERE @ + HERE ! ;\r"
        ": TRUE -1 ;\r"
        ": FALSE 0 ;\r"
        ": 0= 0 = ;\r"
        ": 0< 0 < ;\r"
        ": 0> 0 > ;\r"
        ": <> = 0= ;\r"
        ": <= > 0= ;\r"
        ": >= < 0= ;\r"
        ": 0<= 0 <= ;\r"
        ": 0>= 0 >= ;\r"
        ": 1+ 1 + ;\r"
        ": 1- 1 - ;\r"
        ": 2+ 2 + ;\r"
        ": 2- 2 - ;\r"
        ": 2/ 2 / ;\r"
        ": 2* 2 * ;\r"
        ": D2/ 2. D/ ;\r"
        ": +! DUP @ ROT + SWAP ! ;\r"
        ": [COMPILE] WORD FIND >CFA , ; IMMEDIATE\r"
        ": [CHAR] key ' LIT , , ; IMMEDIATE\r"
        ": RECURSE LATEST @ >CFA , ; IMMEDIATE\r"
        ": DOCOL 0 ;\r"
        ": CONSTANT CREATE DOCOL , ' LIT , , ' EXIT , ;\r"
        ": 2CONSTANT SWAP CREATE DOCOL , ' LIT , , ' LIT , , ' EXIT , ;\r"
        ": VARIABLE CREATE DOCOL , ' LIT , HERE @ 2 CELLS + , ' EXIT , CELL ALLOT ;\r"
        ": 2VARIABLE CREATE DOCOL , ' LIT , HERE @ 2 CELLS + , ' EXIT , 2 CELLS ALLOT ;\r"
        ": IF ' 0BRANCH , HERE @ 0 , ; IMMEDIATE\r"
        ": THEN DUP HERE @ SWAP - SWAP ! ; IMMEDIATE\r"
        ": ELSE ' BRANCH , HERE @ 0 , SWAP DUP HERE @ SWAP - SWAP ! ; IMMEDIATE\r"
        ": BEGIN HERE @ ; IMMEDIATE\r"
        ": UNTIL ' 0BRANCH , HERE @ - , ; IMMEDIATE\r"
        ": AGAIN ' BRANCH , HERE @ - , ; IMMEDIATE\r"
        ": WHILE ' 0BRANCH , HERE @ 0 , ; IMMEDIATE\r"
        ": REPEAT ' BRANCH , SWAP HERE @ - , DUP HERE @ SWAP - SWAP ! ; IMMEDIATE\r"
        ": UNLESS ' 0= , [COMPILE] IF ; IMMEDIATE\r"
        ": DO HERE @ ' SWAP , ' >R , ' >R , ; IMMEDIATE\r"
        ": LOOP ' R> , ' R> , ' SWAP , ' 1+ , ' 2DUP , ' = , ' 0BRANCH , HERE @ - , ' 2DROP , ; IMMEDIATE\r"
        ": +LOOP ' R> , ' R> , ' SWAP , ' ROT , ' + , ' 2DUP , ' <= , ' 0BRANCH , HERE @ - , ' 2DROP , ; IMMEDIATE\r"
        ": I ' R@ , ; IMMEDIATE\r"
        ": SPACES DUP 0> IF 0 DO SPACE LOOP ELSE DROP THEN ;\r"
        ": ZEROS DUP 0> IF 0 DO 48 EMIT LOOP ELSE DROP THEN ;\r"
        ": ABS DUP 0< IF NEGATE THEN ;\r"
        ": DABS 2DUP 0. D< IF DNEGATE THEN ;\r"
        ": .DIGIT DUP 9 > IF 55 ELSE 48 THEN + EMIT ;\r"
        ": .SIGN DUP 0< IF 45 EMIT NEGATE THEN ;\r"
        ": .POS BASE @ /MOD ?DUP IF RECURSE THEN .DIGIT ;\r"
        ": . .SIGN DUP IF .POS ELSE .DIGIT THEN ;\r"
        ": U. BASE @ U/MOD ?DUP IF RECURSE THEN DUP 10 < IF 48 ELSE 10 - 65 THEN + EMIT ;\r"
        ": COUNTPOS SWAP 1 + SWAP BASE @ / ?DUP IF RECURSE THEN ;\r"
        ": UCOUNTPOS SWAP 1 + SWAP BASE @ U/ ?DUP IF RECURSE THEN ;\r"
        ": DIGITS DUP 0< IF NEGATE 1 ELSE 0 THEN SWAP COUNTPOS ;\r"
        ": UDIGITS 0 SWAP UCOUNTPOS ;\r"
        ": .R OVER DIGITS - SPACES . ;\r"
        ": .RZ OVER DIGITS - ZEROS . ;\r"
        ": U.R OVER UDIGITS - SPACES U. ;\r"
        ": U.RZ OVER UDIGITS - ZEROS U. ;\r"
        ": . . SPACE ;\r"
        ": U. U. SPACE ;\r"
        ": ? @ . ;\r"
        ": WITHIN OVER - >R - R> U< ;\r"
        ": DEPTH DSP@ S0@ - ;\r"
        ": .S DSP@ BEGIN DUP S0@ > WHILE DUP ? CELL - REPEAT DROP ;\r"
        ": TYPE 0 DO DUP C@ EMIT 1 + LOOP DROP ;\r"
        ": ALIGNED 1 + 1 INVERT AND ;\r"
        ": DALIGNED 3 + 3 INVERT AND ;\r"
        ": ALIGN HERE @ ALIGNED HERE ! ;\r"
        ": DALIGN HERE @ DALIGNED HERE ! ;\r"
        ": COMPILE-STR ' LITSTRING , HERE @ 0 , BEGIN KEY DUP 34 <> WHILE C, REPEAT DROP DUP HERE @ SWAP - CELL - SWAP ! ALIGN ; \r"
        ": INTERPRET-STR HERE @ BEGIN KEY DUP 34 <> WHILE OVER C! 1+ REPEAT DROP HERE @ - HERE @ SWAP ; \r"
        ": s\" STATE @ IF COMPILE-STR ELSE INTERPRET-STR THEN ; IMMEDIATE\r"
        ": .\" STATE @ IF [COMPILE] s\" ' TYPE , ELSE BEGIN KEY DUP 34 = IF DROP EXIT THEN EMIT AGAIN THEN ; IMMEDIATE\r"
        ": ( BEGIN KEY [CHAR] ) = UNTIL ; IMMEDIATE\r"
        ": COUNT DUP 1+ SWAP C@ ;\r"
        ": MIN 2DUP < IF DROP ELSE NIP THEN ;\r"
        ": MAX 2DUP > IF DROP ELSE NIP THEN ;\r"
        ": D0= OR 0= ;\r"
        ": DMIN 2OVER 2OVER D< IF 2DROP ELSE 2NIP THEN ;\r"
        ": DMAX 2OVER 2OVER D> IF 2DROP ELSE 2NIP THEN ;\r"
        ": ID. 2 + DUP C@ 31 AND BEGIN DUP 0> WHILE SWAP 1 + DUP C@ EMIT SWAP 1 - REPEAT 2DROP ;\r"
        ": ?HIDDEN 2 + C@ 64 AND ;\r"
        ": ?IMMEDIATE 2 + C@ 128 AND ;\r"
        ": WORDS CR LATEST @ BEGIN ?DUP WHILE DUP ?HIDDEN NOT IF DUP ID. SPACE THEN @ REPEAT CR ;\r"
